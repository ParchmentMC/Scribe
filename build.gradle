plugins {
    id 'java'
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}"
    id 'idea'
    id 'org.jetbrains.intellij' version '1.1.4'
    id 'org.cadixdev.licenser' version '0.6.1'
}

group = pluginGroup
version = pluginVersion

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    mavenCentral()
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = 'Minecraft Forge'
        url = 'https://maven.minecraftforge.net'
    }
    maven {
        name = 'IntelliJ Dependencies'
        url = 'https://cache-redirector.jetbrains.com/intellij-dependencies'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"

    implementation "org.parchmentmc.feather:io-moshi:${featherVersion}"
    implementation "com.squareup.moshi:moshi-kotlin:${moshiVersion}"
    implementation "net.minecraftforge:srgutils:${srgutilsVersion}"
}

intellij {
    pluginName = project.properties.pluginName
    type = platformType
    version = platformVersion

    downloadSources = true
    updateSinceUntilBuild = true
    plugins = ['java', 'gradle']
    sandboxDir = layout.projectDirectory.dir(".sandbox").toString()
}

license {
    header = file('HEADER')
    ext {
        name = 'IntelliParchment'
        year = 2021
        holder = 'ParchmentMC'
    }
    exclude '**/bytecode-utils.kt', '**/class-utils.kt', '**/MemberReference.kt', '**/psi-utils.kt', '**/gradle-util.kt'
}

idea.module.excludeDirs.add(project.file(intellij.sandboxDir.get()))

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=all']
    }
}

compileGroovy {
    dependsOn tasks.getByPath('compileKotlin')
    classpath += files(compileKotlin.destinationDir)
}
